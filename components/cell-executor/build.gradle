description = 'Cell executor'

apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-java-application'

mainClassName = "org.jboss.weld.environment.se.StartMain"

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:2.6.5'
    }
}

dependencies {

    compile project(':common')
    compile project(':common-utils')
    compile project(':common-camel')
    compile project(':core-services-common')
    compile project(':core-services-exec')
    compile project(':core-services-client')

    compile 'org.jboss.weld.se:weld-se:2.3.2.Final'

    compile "org.apache.camel:camel-core:$camelVersion"
    compile "org.apache.camel:camel-rabbitmq:$camelVersion"
    compile "org.apache.camel:camel-jackson:$camelVersion"
    compile "io.astefanutti.camel.cdi:camel-cdi:1.2.0"

    compile 'com.sun.jersey:jersey-client:1.19'
    compile 'com.sun.jersey:jersey-json:1.19'

    compile "org.codehaus.groovy:groovy-all:$groovyVersion"
}

sourceSets.main.resources {
    exclude 'META-INF/beans.xml'
}
classes {
    inputs.dir 'src/main/resources/META-INF/beans.xml'
    outputs.dir "$buildDir/classes/main/META-INF/beans.xml"
} << {
    copy {
        from('src/main/resources') { include 'META-INF/beans.xml' }
        into "$buildDir/classes/main/"
    }
}

testClasses {
    inputs.dir 'src/test/resources/META-INF/beans.xml'
    outputs.dir "$buildDir/classes/test/META-INF/beans.xml"
} << {
    copy {
        from('src/test/resources') { include 'META-INF/beans.xml' }
        into "$buildDir/classes/test/"
    }
}

docker {
    url = (System.getenv('DOCKER_HOST') ?: 'unix:///var/run/docker.sock')
    certPath = null

    javaApplication {
        baseImage = 'java:openjdk-8-jre'
        maintainer = 'Tim Dudgeon <tdudgeon@informaticsmatters.com>'
        tag = 'squonk/cellexecutor'
    }
}
