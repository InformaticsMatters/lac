apply plugin: 'application'
mainClassName = "com.im.lac.services.chemaxon.Main"
applicationDefaultJvmArgs = ["-Xmx400m"]
def appName = "chemaxon-services-basic"
applicationName = appName

apply plugin: 'docker'

buildscript {
    repositories { 
        jcenter() 
    }
    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

dependencies {

    compile project(':common')
    compile project(':chemaxon-lib')
    compile project(':chemaxon-camel')
    
    compile "org.apache.camel:camel-core:$camelVersion"
    compile "org.apache.camel:camel-jetty:$camelVersion"
    compile "org.apache.camel:camel-jackson:$camelVersion"
    
    testCompile project(':core-test')
}

docker {
    baseImage "java:openjdk-8-jre"
    maintainer 'Tim Dudgeon'
}

/** Build a Docker image
 * run the generated image with something like this:
 * docker run -d --name chemaxon-services-basic -p 8080:8080 <image_id>
 * 
 * The following environment variables can be used to override the default settings:
 * CHEMAXON_LICENSE_URL
 */
task buildDocker(type: Docker, dependsOn: distZip) {
    
    addFile('../../../data/licenses/license.cxl', 'license.cxl')
    def zipFile = tasks.distZip.outputs.files.singleFile
    addFile(zipFile, zipFile.name)
    runCommand("unzip " + zipFile.name)
    
    setEnvironment("CHEMAXON_LICENSE_URL", "license.cxl")
    exposePort(8080) // do we need to configure the port?
    
    def executable =  "${appName}-${version}/bin/${appName}"
    //println "Executable: $executable"
    entryPoint([executable])
}
