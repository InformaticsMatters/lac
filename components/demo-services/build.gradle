

apply plugin: 'groovy'

apply plugin: 'application'
mainClassName = "com.im.lac.demo.routes.Main"
applicationDefaultJvmArgs = ["-Xmx1200m"]

apply plugin: 'docker'


buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}


dependencies {


    compile project(':chemaxon-camel')
    compile project(':chemaxon-lib')
    compile project(':cdk-camel')
    compile project(':cdk-lib')
    compile project(':common')

    //compile 'org.slf4j:slf4j-simple:1.7.7'
    
    compile "org.postgresql:postgresql:9.3-1102-jdbc4"
    
    compile "org.apache.camel:camel-core:$camelVersion"
    //compile "org.apache.camel:camel-restlet:$camelVersion"
    compile "org.apache.camel:camel-jetty:$camelVersion"
    compile "org.apache.camel:camel-jackson:$camelVersion"
    
    compile 'javax.ws.rs:javax.ws.rs-api:2.0.1'
    compile 'org.glassfish.jersey.core:jersey-server:2.6'

    //testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
    //testCompile "org.apache.derby:derby:10.11.1.1"
    testCompile project(':core-test')
}



docker {
    baseImage "dockerfile/java:oracle-java8"
    maintainer 'Tim Dudgeon'
}

/** Build a Docker image
 * run the generated image with something like this:
 * docker run -d --name services -p 80:8080 --link <name_of_postgres_db_image>:db -v <host_dir>:/external <image_id>
 * 
 * Note: the host directory <host_dir> containing the ChemAxon license file needs to be specified,
// * or some other mechanism used, probably by setting the value of the CHEMAXON_LICENSE_URL
 * environment variable using an -e option.
 * The following environment variables can be used to override the default settings:
 * CHEMCENTRAL_DB_SERVER, CHEMCENTRAL_DB_PORT, CHEMCENTRAL_DB_USERNAME, CHEMCENTRAL_DB_PASSWORD, CHEMAXON_LICENSE_URL
 */
task buildDocker(type: Docker, dependsOn: distZip) {
    applicationName = "demoservices"
    def zipFile = tasks.distZip.outputs.files.singleFile
    addFile(zipFile, '/data/' + zipFile.name)
    runCommand("unzip /data/" + zipFile.name)
    addFile {
        into "pages"
        from "src/main/html" 
    }
    setEnvironment("HTML_DOC_ROOT", "/pages")
    setEnvironment("CHEMCENTRAL_DB_SERVER", "db") // assumes the db alias is used when linking. If not override with a -e argument
    setEnvironment('CHEMCENTRAL_DB_PORT', '5432')
    setEnvironment("CHEMAXON_LICENSE_URL", "/external/license.cxl")
    exposePort(8080) // do we need to configure the port?
    entryPoint(["/data/${project.name}-${version}/bin/${project.name}"])
}
