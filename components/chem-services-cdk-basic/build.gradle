description = 'Basic services based on Chemistry Development Kit'
def contextName = 'cdk_basic_services'

apply plugin: 'war'
apply plugin: 'com.bmuschko.tomcat'
apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

dependencies {
    compile project(':common')
    compile project(':common-camel')
    compile project(':core-services-common')
    compile project(':cdk-lib')
    compile project(':cdk-camel')
    
    compile "org.apache.camel:camel-core:$camelVersion"
    compile "org.apache.camel:camel-jackson:$camelVersion"
    compile "org.apache.camel:camel-servletlistener:$camelVersion"
    compile "org.apache.camel:camel-servlet:$camelVersion"
    compile "org.apache.camel:camel-swagger:$camelVersion"
    
    providedCompile 'javax.servlet:javax.servlet-api:3.0.1'
    
    def tomcatVersion = '7.0.59'
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
           "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}",
           "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"
}

tomcat {
    enableSSL = false
}

docker {
    url = dockerHost
    certPath = null
}

task dockerFile(type: Dockerfile, dependsOn: war) {
    
    destFile = project.file('build/Dockerfile')
    from "tomcat:7-jre8"
    maintainer 'Tim Dudgeon <tdudgeon@informaticsmatters.com>'
    
    runCommand("rm -rf /usr/local/tomcat/webapps/*")
    
    def warFile = tasks.war.outputs.files.singleFile
    addFile('libs/' + warFile.name, "/usr/local/tomcat/webapps/${contextName}.war")
    
    exposePort(8080)
}

task dockerImage(type: DockerBuildImage) {
    dependsOn war, dockerFile
    inputDir = dockerFile.destFile.parentFile
    tag = "squonk/${contextName}"
}
